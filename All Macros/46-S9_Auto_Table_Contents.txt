Option Explicit

Sub Auto_Table_Contents()
    
    Dim StartCell As Range  ' For inputbox to select range
    Dim EndCell As Range    ' for message box as info
    Dim Sh As Worksheet
    Dim ShName As String
    Dim MsgConfirm As VBA.VbMsgBoxResult    ' For message box to confirm
    
    On Error Resume Next
    
    Set StartCell = Excel.Application.InputBox("Where do you want to insert the Table of Contents?" _
    & vbNewLine & "Please select a cell:", Title:="Insert Table of Contents", Type:=8)
    
    If Err.Number = 424 Then Exit Sub
    
    On Error GoTo Handle
    
'    Debug.Print StartCell.Address
    Set StartCell = StartCell.Cells(1, 1)
'    Debug.Print StartCell.Address
    Set EndCell = StartCell.Offset(Worksheets.Count - 2, 1)
'    Debug.Print EndCell.Address

    ' Get confirmation
    MsgConfirm = VBA.MsgBox("The values in cells:" & vbNewLine & StartCell.Address & " to " & _
                 EndCell.Address & " could be overwriteen." & vbNewLine & _
                 "Would you like to continue?", vbOKCancel + vbDefaultButton2, _
                 Title:="Confirmation Required!")
    
    If MsgConfirm = vbCancel Then Exit Sub
    
    For Each Sh In Worksheets
        ShName = Sh.Name
        If ActiveSheet.Name <> ShName Then
            If Sh.Visible = xlSheetVisible Then
                ActiveSheet.Hyperlinks.Add Anchor:=StartCell, Address:="", _
                SubAddress:="'" & ShName & "'!A1", TextToDisplay:=ShName
    
                StartCell.Offset(0, 1).Value = Sh.Range("A1").Value
                Set StartCell = StartCell.Offset(1, 0)
            End If  ' Sheet is visible
        End If  ' Sheet is not activesheet
    
    Next Sh
Exit Sub

Handle:
    MsgBox "Unfortunately, an error has occurred!"
End Sub
