
Sub Input_Sales_Word()
    Dim WordApp As Word.Application
    Dim WordDoc As Word.Document
    Dim r As Long
    Dim FileName As String
    
    On Error Resume Next
    Set WordApp = GetObject(, "Word.Application")
    On Error GoTo 0
    If WordApp Is Nothing Then
        Set WordApp = New Word.Application
    End If
    WordApp.Visible = True
    
    FileName = ThisWorkbook.Path & "\Letter_"
    'use a Do Loop
    r = 10
    Do Until IsEmpty(Cells(r, 1))
        Set WordDoc = WordApp.Documents.Open(ThisWorkbook.Path & "\Letter_Word.docx")
        WordDoc.Bookmarks("PersonName").Range.InsertAfter Cells(r, 1).Text
        WordDoc.Bookmarks("SalesValue").Range.InsertAfter Cells(r, 2).Text
        WordDoc.SaveAs2 FileName & Cells(r, 1).Text & ".docx"
        WordDoc.Close
        
        r = r + 1
    Loop
    
    Set WordApp = Nothing
    
    MsgBox "Personalized letters have been created." & vbNewLine & _
    "They are saved in same folder as this Workbook.", vbInformation, "Job Done"
End Sub